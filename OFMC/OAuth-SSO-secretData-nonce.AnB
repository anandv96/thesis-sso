Protocol: OAuth

Types: Agent U, idp, RP;
       Number SecretData, STATE, NU, NU2, NU3, NRP, NRP2, NRP3, NRP4;
       Function h, pw, cpw, info;

Knowledge: U: U, idp, RP, pw(U);
	  idp: U, idp, RP, info(U), h(pw(U)), h(cpw(RP));
	   RP: idp, RP, cpw(RP);
	   
   where RP!=U, RP!=idp, U!=idp
Actions:

[U] *->* RP  : U, RP, idp, NU
RP *->* [U] : STATE, RP, NU

[U] *->* idp : U, RP, idp, STATE, pw(U), NRP
idp *->* [U] : STATE, Code, NRP

[U] *->* RP  : STATE, Code, NU2, NU3

[RP]*->* idp : cpw(RP), Code, NRP2
idp *->* [RP]: Token, NRP2

[RP]*->* idp : Token, NRP3
idp *->* [RP]: U, info(U), RP, idp, NRP3

[RP] *->* U  : U, RP, NRP4, NU2
U *->* [RP]  : U, RP, NRP4, NU2, NU3
[RP] *->* U  : SecretData


Goals:

RP authenticates idp on U, info(U), Code, Token
RP authenticates U on NU2
SecretData secret between RP, U
Token secret between RP, idp
